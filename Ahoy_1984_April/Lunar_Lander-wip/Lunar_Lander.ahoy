5 REM DEC. 19, 1983
6 POKE 53280,8:POKE53281,0
10 PRINT"{SC}{WH}{CD}{CD}{CD}{CD}             LUNAR LANDER"
20 PRINT"{CD}          (C) COPYRIGHT 1983"
30 PRINT"            BY ANTHONY WOOD"
50 POKE55,248:POKE56,157:REM 40440
60 CLR:S=54272
100 V=53248:S1=64*13:S2=64*14:S3=64*15:K=2040:SR=40444
110 FORZ=0to39:READA:POKE40456+Z,A
120 POKES2+Z,A:POKES3+Z,A:NEXT
130 FOR Z=40TO62:READA:POKE40456+Z,A:NEXT
132 FORZ=40TO62:READA:POKES2+Z,A:NEXT
134 FORZ=40TO62:READA:POKES3+Z,A:NEXT
135 FORZ=1TO63*3:READA:POKE40518+Z,A:NEXT
137 FORZ=0TO13:READA:POKE40441+Z,A:NEXT
138 SYS40441
139 FU=5000:F2=0:L1=0:L2=0:SC=0:GOSUB700
140 GOSUB900:POKEV+39,1:POKEK,13:POKEV,0:POKEV+1,0:POKEV+21,1
150 H=15:E=5:X=30+INT(RND(0)*100):Y=30:F(0)=13:F(1)=13:F(2)=14
160 F(3)=15:M=1
200 J=PEEK(56320):P=PEEK(197):IFFU=0THENB=0:M=1:POKES+1,0:GOTO220
210 IFP=60OR(JAND16)=0THENB=B+1:GOTO213
211 B=0:M=1:POKES+4,0
213 IFM<>1THENPOKES+4,129
215 IF P=7OR(JAND4)=0THENH=H-1:PRINT"{HM}{CD}{CR}<<":FU=FU-1
216 IF P=2OR(JAND8)=0THENH=H+1:PRINT"{HM}{CD}{CR}>>":FU=FU-1
220 GOSUB760:IFB=6ORB=1THENM=M+1
240 E=E+GR-M*1.65:FU=FU-(M-1)*10:IFFU<0THENFU=0
245 X=X+INT(H/6):Y=Y+INT(E/5)
250 IFX<5THENX=5
252 IFY>251THENY=252:GOSUB800:Y=35:X=29:FU=5000:F2=1:GOSUB5000
255 IFY<35THENY=35:E=3
256 IFX>319THENX=319
260 POKEK,13:Z=PEEK(V+31):GOSUB800
266 IFPEEK(V+31)<>0THEN300
270 POKEK,F(M-FL):IFFL=0THENFL=1:GOTO200
280 FL=0:GOTO200
300 IFF2=1THEN440
310 IFY<149THEN270
320 IFY<227 ORX<099OR X>123 ORE>9ORABS(H)>10THEN330
322 IF Ll=1ANDL2=1THEN330
325 Y=227:GOSUB800:SC=SC+100:L2=1
327 GOTO400
330 IFY<177 ORX<197 ORX>203 ORE>9 ORABS(H)>10THEN340
335 Y=179:GOSUB800:SC=SC+500:L1=1:GOTO400
340 POKES+4,129:V2=11:POKESR,63+8:SYS40441:GOSUB820:POKESR,126+8:SYS40441
350 GOSUB840:POKESR,189+8:SYS40441:GOSUB840:POKES+4,0:POKES+24,15:POKES+1,2
360 POKEV+21,0:POKESR,8:SYS40441:GOTO410
400 POKES+4,0:GOSUB750:PRINT"{HM} LANDED!!"
410 GETA$:IFA$<>"{F7}"THEN410
415 POKEV+21,0:PRINT"{HM}       "
420 IFFU=0THEN 460
425 IFF2=1THENY=10:X=29:E=5:H=0:GOSUB5000:POKEV,X:POKEV+1,Y:POKEV+21,1:GOTO160
435 GOTO140
440 IFX<230ORX>242ORY<121ORY>130ORE>9ORABS(H)>11THEN340
450 F2=0:SC=SC+1000:L1=0:L2=0:FU=4500:GOTO400
460 PRINT"{SC}":PRINT:PRINT"DO YOU WANT TO PLAY AGAIN?"
470 GETA$:IFA$=""THEN470
480 IFA$="Y"THEN139
490 IFA$<>"N"THEN470
500 POKES+4,0:PRINT"{SC}":END
700 PRINT:PRINT:PRINT"  ENTER GRAVITY STRENGTH ({RV}1{RO},{RV}2{RO}, OR {RV}3{RO})"
701 POKES+5,129:POKES+6,216:POKES+24,15          .
702 POKES,200:POKES+4,0:POKES+1,2
710 GETA$:IFA$=""THEN710
715 IFA$="1"THENGR=3:RETURN
720 IFA$="2"THENGR=3.6:RETURN
730 IFA$="3"THENGR=4.25: RETURN
740 GOTO710
750 PRINT"{HM}{CR}{CR}{CR}{CR}{CR}{CR}{CR}{CR}{CR}{CR}{CR}{CR}{CR}{CR}{CR}{CR}{CR}SCORE";SC;
760 PRINT"{HM}{CD}{CR}  {HM}";TAB(28);"FUEL     {CL}{CL}{CL}{CL}{CL})";FU;:RETURN
800 POKEV+1,Y:IFX<256THENPOKEV,X:POKEV+16,0:RETURN
810 POKEV,X-256:POKE53264,127:RETURN
820 POKES+1,3:FORX=1TO4:POKES+24,V2:FORY=1TO15:NEXT:V2=V2+1
830 NEXT:RETURN
840 FORX=1TO7:POKES+24,V2:FORY=1TO9:NEXT:V2=V2-1:NEXT:RETURN
900 PRINT"{SC}":GOSUB750:PRINT:FORY=0TO9:A=RND(0)
910 IFA>.3THENPRINTTAB(RND(0)*39);"."
920 IF A<=.3THENPRINT
930 NEXT:PRINT:PRINT:PRINT
1000 PRINT"                              {RV}{SHIFT-POUND}{C=-*}{RO}    "
1100 PRINT"                             {RV}{SHIFT-POUND}  {C=-*}{RO}     "
1200 PRINT"                           {RV}{SHIFT-POUND}      {C=-*}{RO}   "
1300 PRINT"                         {RV}{SHIFT-POUND}         {C=-*}{RO}  "
1400 PRINT"                     {RV}{SHIFT-POUND}              {RO}  "
1500 PRINT"                    {RV}{SHIFT-POUND} 500           {C=-*}{RO}"
1600 PRINT"      {RV}{SHIFT-POUND} {C=-*}{RO}           {RV}{SHIFT-POUND}                  {RO} "
1700 PRINT"     {RV}{SHIFT-POUND}   {RO}           {RV}{SHIFT-POUND}                   {C=-*}{RO} "
1800 PRINT"   {RV}{SHIFT-POUND}      {C=-*}{RO}      {RV}{SHIFT-POUND}                   {RO}  "
1900 PRINT"  {RV}{SHIFT-POUND}        {C=-*}{RO}    {RV}{SHIFT-POUND}                     {C=-*}{RO}  "
1910 PRINT"{RV}           100                          {RO}";
1920 IFL2=1ANDL1=1THENPRINT"{HM}{CD}{CD}{CD}{CD}{CD}{CD}{CD}{CD}{CD}{CD}{CD}{CD}{CD}{CD}{CD}{CD}{CD}{CD}{CD}{CD}{CD}{CD}{CD}{CD}{CR}{CR}{CR}{CR}{CR}{CR}{CR}{CR}{CR}{CR}{RV}{C=-*}{RO}   {RV}{SHIFT-POUND}{RO}";
1930 RETURN
1999 REM LANDER SPRITES
2000 DATA 0,255,0,0,255,0,7,255,224,7
2010 DATA 255,224,7,255,224,0,255,0,0,255
2020 DATA 0,15,255,240,15,255,240,15,255,240
2030 DATA 3,255,192,3,60,192,6,12,6,96,12
2040 REM NO FLAME
2050 DATA 0,48,24,0,24,0,0,0,0,0
2060 DATA 0,0,0,0,0,0,0,0,0,0,0,0,0
2070 REM SMALL FLAME
2080 DATA 0,48,24,60,24,0,102,0,0,60
2090 DATA 0,0,24,0,0,24,0,0,0,0,0,0,0
2100 REM BIG FLAME
2110 DATA 0,48,24,126,24,0,219,0,0,219
2120 DATA 0,0,102,0,0,60,0,0,24,0,0,24,0
2130 REM EXPLOSION
2140 DATA 0,0,0,0,0,0,0,0
2150 DATA 0,0,0,0,0,0,0,0
2160 DATA 64,0,6,72,0,0,24,0
2170 DATA 12,224,0,0,228,0,0,0
2180 DATA 0,13,80,0,0,0,0,0
2190 DATA 0,0,0,0,0,0,0,0,0,0,0,0,0
2200 DATA 0,0,0,0,0,0,0,0,0,0
2210 REM FRAME 2 OF EXPLOSION
2220 DATA 0,0,0,0,0,0,0,0
2230 DATA 0,0,128,0,24,131,0,24
2240 DATA 3,0,0,0,0,0,0,0
2250 DATA 48,224,0,48,227,0,0,0
2260 DATA 0,0,0,0,28,0,0,24
2270 DATA 152,0,2,8,0,0,64,0
2280 DATA 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
2290 REM FRAME 3 OF EXPLOSION
2300 DATA 0,0,0,0,0,0,0,0
2310 DATA 0,0,128,128,24,129,0,24
2320 DATA 0,0,0,0,0,0,0,0
2330 DATA 64,0,0,64,0,112,0,0
2340 DATA 0,0,0,0,32,0,208,32
2350 DATA 0,0,0,0,0,0,2,0
2360 DATA 4,65,0,4,64,128,0,0
2370 DATA 0,0,0,0,0,0,0
2380 REM BLOCK MOVE
2390 DATA 162,0,189,8,158,157,64,3,232
2400 DATA 224,63,208,245,96
4999 REM DRAW CAVE
5000 PRINT"{SC}":GOSUB750:PRINT:POKES+4,0:POKES+1,2
5005 PRINT"     {RV}                                  "
5010 PRINT"     {RV}                                  "
5020 PRINT"     {RV}                                  "
5030 PRINT"     {RV}                                  "
5040 PRINT"     {RV}                                  "
5050 PRINT"     {RV}                    {RO}{SHIFT-POUND}  {C=-*}{RV}  {RO}{SHIFT-POUND}  {C=-*}{RV}  "
5060 PRINT"     {RV}                    {RO}              "
5065 PRINT"     {RV}                    {RO}              "
5070 PRINT"     {RV}                    {RO}              "
5080 PRINT"     {RV}                    {RO}  {C=-P}{C=-P}{C=-P}{C=-P}           "
5090 PRINT"     {RV}                         {RO}         "
5100 PRINT"     {RV}              1000      {RO})          "
5200 PRINT"     {RV}                       {RO})           "
5210 PRINT"     {RV}                      {RO})            "
5220 PRINT"     {RV}                     {RO})             "
5230 PRINT"     {C=-*}{SHIFT-POUND}{C=-*}{SHIFT-POUND}{C=-*}{SHIFT-POUND}{C=-*}{SHIFT-POUND}{RV}  {RO}{SHIFT-POUND}{C=-*}  {RO}{SHIFT-POUND}{C=-*}{SHIFT-POUND}{C=-*}{RV}  {RO}{SHIFT-POUND}{C=-*}{SHIFT-POUND}      "
5240 PRINT"                 {C=-*}{SHIFT-POUND}                    {RV}{SHIFT-POUND} {RO}"
5250 PRINT"                                    {RV}{SHIFT-POUND}  {RO}"
5260 PRINT"                                   {RV}{SHIFT-POUND}   {RO}"
5270 PRINT"                                  {RV}{SHIFT-POUND}    {RO}"
5280 PRINT"                                {RV}{SHIFT-POUND}      {RO}"
5285 PRINT"                 {RV}{SHIFT-POUND}{C=-*}{RO}               {RV}           {RO}"
5290 PRINT"     {RV}                                {RO}"
5295 PRINT"     {RV}                                {RO}";
5300 PRINT"{HM}{CD}{CD}{CD}";TAB(38);"{RV} {CD}{CL} {CD}{CL} {CD}{CL} {CD}{CL} {CD}{CL} {CD}{CL} {CD}{CL} {CD}{CL} {CD}{CL} {CD}{CL} {CD}{CL} {CD}{CL} {CD}{CL} {CD}{CL} {CD}{CL} {RO}";
6000 RETURN
